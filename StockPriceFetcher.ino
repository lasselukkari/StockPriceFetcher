#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include <WiFiClientSecureBearSSL.h>
#include <ArduinoJson.h>
#include <SSD1306Wire.h>

#define WIFI_SSID ""
#define WIFI_PASSWORD ""
#define TOKEN "pk_"
#define STOCK_SYMBOL "GME"
#define INTERVAL 60000
#define DISPLAY_WIDTH 64
#define DISPLAY_GEOMETRY GEOMETRY_64_48
#define ROW_HEIGHT 24
#define FONT ArialMT_Plain_16
#define VALUE_COUNT 2

struct value {
  const char* title;
  const char* key;
  int multiplier;
  int digits;
};

value values[VALUE_COUNT] = {
  {title: "$", key: "latestPrice", multiplier: 1, digits: 5},
  {title: "%", key: "changePercent", multiplier: 100, digits: 3},
};

HTTPClient https;
std::unique_ptr<BearSSL::WiFiClientSecure>client(new BearSSL::WiFiClientSecure);
SSD1306Wire display(0x3c, SDA, SCL, DISPLAY_GEOMETRY);
const char * endpoint = "https://cloud.iexapis.com/stable/stock/" STOCK_SYMBOL "/quote?token=" TOKEN;

const unsigned char diamondHands [] = {
  0x00, 0x00, 0x80, 0x01, 0x00, 0x00, 0x00, 0x00, 0x80, 0x01, 0x00, 0x00,
  0x00, 0x80, 0x81, 0x81, 0x01, 0x00, 0x00, 0x00, 0x81, 0x81, 0x00, 0x00,
  0x00, 0x00, 0x83, 0xC1, 0x00, 0x00, 0x00, 0x00, 0x02, 0x40, 0x00, 0x00,
  0x00, 0x00, 0x02, 0x40, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
  0x00, 0xF0, 0xFF, 0xFF, 0x0F, 0x00, 0x00, 0xF0, 0xFF, 0xFF, 0x0F, 0x00,
  0x00, 0x78, 0xC0, 0x03, 0x1E, 0x00, 0x00, 0xC8, 0x60, 0x06, 0x13, 0x00,
  0x00, 0x8C, 0x31, 0x8C, 0x31, 0x00, 0x00, 0x04, 0x1B, 0xD8, 0x20, 0x00,
  0x00, 0x06, 0x0E, 0x70, 0x60, 0x00, 0x0E, 0xFE, 0xFF, 0xFF, 0x7F, 0x70,
  0x1B, 0x06, 0x0C, 0x30, 0x60, 0xD8, 0x11, 0x0C, 0x0C, 0x30, 0x30, 0x88,
  0x11, 0x18, 0x0C, 0x30, 0x18, 0x88, 0x11, 0x18, 0x08, 0x10, 0x18, 0x88,
  0x11, 0x30, 0x18, 0x18, 0x0C, 0x88, 0x11, 0x60, 0x18, 0x18, 0x06, 0x88,
  0xD1, 0xC1, 0x10, 0x08, 0x83, 0x8B, 0x31, 0xC3, 0x30, 0x0C, 0xC3, 0x8C,
  0x31, 0x82, 0x31, 0x8C, 0x41, 0x8C, 0x31, 0x06, 0x23, 0xC4, 0x60, 0x8C,
  0x71, 0x0C, 0x26, 0x64, 0x30, 0x8E, 0x61, 0x08, 0x66, 0x66, 0x10, 0x86,
  0xC3, 0x18, 0x6C, 0x36, 0x18, 0xC3, 0x86, 0x31, 0x58, 0x1A, 0x8C, 0x61,
  0x0C, 0x61, 0xF0, 0x0F, 0x86, 0x30, 0x08, 0xC0, 0xE0, 0x07, 0x03, 0x10,
  0x18, 0x80, 0xE0, 0x07, 0x01, 0x18, 0x30, 0x80, 0xC0, 0x03, 0x01, 0x0C,
  0x60, 0x80, 0x80, 0x01, 0x01, 0x06, 0x40, 0x80, 0x00, 0x00, 0x01, 0x02,
  0x40, 0x80, 0x00, 0x00, 0x01, 0x02, 0xC0, 0x80, 0x00, 0x00, 0x01, 0x03,
  0x80, 0x80, 0x00, 0x00, 0x01, 0x01, 0x80, 0x80, 0x00, 0x00, 0x01, 0x01,
  0xF0, 0xFF, 0x03, 0xC0, 0xFF, 0x0F, 0x10, 0x00, 0x02, 0x40, 0x00, 0x08,
  0x10, 0x00, 0x02, 0x40, 0x00, 0x08, 0x10, 0x60, 0x02, 0x40, 0x06, 0x08,
  0x10, 0x60, 0x02, 0x40, 0x06, 0x08, 0x10, 0x00, 0x02, 0x40, 0x00, 0x08,
  0x10, 0x00, 0x02, 0x40, 0x00, 0x08, 0xF0, 0xFF, 0x03, 0xC0, 0xFF, 0x0F,
};

void drawDiamondHands() {
  int offset = (DISPLAY_WIDTH - 48) / 2;
  display.clear();
  display.drawXbm(offset, 0, 48, 48, diamondHands);
  display.display();
}

void drawRow(int row, const char* title, float value, int digits) {
  char buffer [16] {};
  gcvt(value, digits, buffer);
  int offset = row * ROW_HEIGHT;
  display.setTextAlignment(TEXT_ALIGN_LEFT);
  display.drawString(0, offset, title);
  display.setTextAlignment(TEXT_ALIGN_RIGHT);
  display.drawString(DISPLAY_WIDTH, offset, buffer);
}

void update() {
  drawDiamondHands();

  if (!https.begin(*client, endpoint)) {
    Serial.println("Failed to begin API request");
    return;
  }

  int status = https.GET();
  if (status != HTTP_CODE_OK) {
    Serial.print("API request failed: ");
    Serial.println(status);
    return;
  }

  String body = https.getString();
  https.end();

  Serial.println("Response: ");
  Serial.println(body);
  Serial.println();

  StaticJsonDocument<256> filter;
  for (int i = 0; i < VALUE_COUNT; i++) {
    filter[values[i].key] = true;
  }

  StaticJsonDocument<512> json;
  DeserializationError error = deserializeJson(json, body, DeserializationOption::Filter(filter));
  if (error) {
    Serial.println("Failed to deseiralice JSON:");
    Serial.println(body);
    return;
  }

  Serial.println("Parsed JSON: ");
  serializeJsonPretty(json, Serial);
  Serial.println();

  display.clear();
  for (int i = 0; i < VALUE_COUNT; i++) {
    float value = json[values[i].key];
    drawRow(i, values[i].title, value * values[i].multiplier, values[i].digits);
  }
  display.display();
  return;
}

void setup() {
  Serial.begin(115200);
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  client->setInsecure();
  display.init();
  display.setFont(FONT);
  Serial.println();
}

void loop() {
  if (WiFi.status() == WL_CONNECTED) {
    update();
    delay(INTERVAL);
  } else {
    drawDiamondHands();
    delay(1000);
  }
}
